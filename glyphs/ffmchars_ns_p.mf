% nonscalable letters (preview)

% digits

ffmchar("zero",11,ht#,0);
italcorr .6ht#*slant;
x1=.5w+noise;
x2=good.x(2u+s+noise);
x3=.5w+noise; 
w-x4=good.x(2u+s+noise);  
top y1=h+o+noise; 
y2=barheight+noise;
bot y3=noise-o;
y4=barheight+noise;
draw full(z1,-randrt,z2,-randup,z3,randrt,z4,randup);
labels(1,2,3,4); 
endchar;

ffmchar("one",11,ht#,0);
italcorr .7ht#*slant;
x1=leftstemloc+noise; 
w-x2=good.x(4.5u+s+noise);
w-x3=good.x(4.5u+s+noise);
y1=.618h+noise; 
top y2=h+o+noise;
bot y3=noise-o;
draw z1--z2--z3;
labels(1,2,3); 
endchar;

ffmchar("two",11,ht#,0);
italcorr .8ht#*slant;
x1=leftstemloc-ho+noise;
x5=good.x(2u+s+noise);
x2=.5[x1,x6]+noise;
w-x3=leftstemloc+noise;
x4=.5[x1,x6]+noise;
w-x6=leftstemloc-ho+noise; 
top y2=h+noise;
y3=.5[y4,y2]+noise;
y4=barheight+noise; 
bot y5=noise; 
bot y6=noise; 
z1=z2+whatever*randrt;
pair randir;
randir:=-randrt;
draw z1--z2 
 & half(z2,z2-z1,z3,-randup,z4,randir)
 & arc(z4,randir,z5,-randup)
 & z5--z6;
labels(1,2,3,4,5,6); 
endchar;

ffmchar("three",11,ht#,0);
italcorr .8ht#*slant;
x1=leftstemloc-ho+noise;
x8=leftstemloc-ho+noise;
w-x3=good.x(2u+s+noise);
w-x6=good.x(2.5u+s+noise);
x5=.618[x2,x1]+noise;
x2=.55w+noise;
x4=.55w+noise;
x7=.55w+noise;
bot y1=noise;
bot y2=noise; 
top y7=h+noise;
top y8=h+noise;
y4=barheight+.5noise; 
y5=barheight+.5noise; 
y3=.5[y2,y4]+noise;
y6=.5[y4,y7]+noise;
draw z1--z2
 & half(z2,z2-z1,z3,randup,z4,z5-z4)
 & z4--z5;
draw half(z4,z4-z5,z6,randup,z7,z8-z7)
 & z7--z8;
labels(1,2,3,4,5,6,7,8); 
endchar;

ffmchar("four",11,ht#,0);
italcorr .7ht#*slant;
x2=good.x(1.5u+s+noise);
w-rt x3=eps+noise;
w-x4=good.x(3.5u+s+noise);
w-x5=good.x(3.5u+s+noise);
rt x1+.5u=lft x4+noise;
y4=.618h+noise; 
top y1=h+o+noise; 
bot y5=noise-o;
y2=.618[y4,y5]+noise;
y3=.618[y4,y5]+noise;
draw z1--z2--z3;
draw z4--z5;
labels(1,2,3,4,5); 
endchar;

ffmchar("five",11,ht#,0);
italcorr .8ht#*slant;
x5=leftstemloc+noise; 
x6=leftstemloc+noise; 
x7=w-x5+noise; 
x1=leftstemloc-ho+noise; 
w-x3=good.x(2u+s+noise);
x2=.5[x5,x3]+noise;
x4=.5[x5,x3]+noise;
bot y1=noise; 
bot y2=noise; 
top y6=h+noise;
top y7=h+noise;
y4=barheight+.5noise; 
y5=barheight+.5noise; 
y3=.5[y2,y4]+noise;
draw z1--z2
 & half(z2,z2-z1,z3,randup,z4,z5-z4)
 & z4--z5--z6--z7;
labels(1,2,3,4,5,6,7); 
endchar;

ffmchar("six",11,ht#,0);
italcorr .8ht#*slant;
x1=.5[x2,x4]+noise;
x2=good.x(2u+s+noise);
x3=.5[x2,x4]+noise; 
w-x4=good.x(1.75u+s+noise);  
x5=.618[x2,x4]+noise;
w-x6=leftstemloc+noise;
y1=barheight+noise; 
y2=.5[y1,y3]+noise;
bot y3=noise-o;
y4=.5[y1,y3]+noise;
top y5=h+noise;
z6=z5+whatever*randrt;
pair randir;
randir:=randup;
draw full(z1,-randrt,z2,-randir,z3,randrt,z4,randup);
draw arc(z2,randir,z5,z6-z5)
 & z5--z6;
labels(1,2,3,4,5,6); 
endchar;

ffmchar("seven",11,ht#,0);
italcorr ht#*slant;
x1=good.x(2u+s+noise);
w-x2=good.x(2u+s+noise);
x3=.618[x2,x1]+noise;
top y1=h+noise; 
top y2=h+noise; 
bot y3=noise-o;
draw z1--z2--z3;
labels(1,2,3); 
endchar;

ffmchar("eight",11,ht#,0);
italcorr .7ht#*slant;
x1=.5w+noise;
x3=.5w+noise;
x6=.5w+noise;
x2=good.x(1.5u+s+noise);
w-x4=good.x(1.5u+s+noise);
x7=.2[x2,x1]+noise;
w-x5=.2[x2,x1]+noise;
y1=barheight+noise; 
bot y3=noise-o;
top y6=h+o+noise;
y2=.5[y1,y3]+noise; 
y4=.5[y1,y3]+noise; 
y7=.5[y1,y6]+noise;
z5=z7+whatever*(z4-z2);
pair randir;
randir:=randrt;
draw full(z1,-randir,z2,-randup,z3,randrt,z4,randup);
draw full(z1,randir,z5,randup,z6,-randrt,z7,-randup);
labels(1,2,3,4,5,6,7); 
endchar;

ffmchar("nine",11,ht#,0);
italcorr .7ht#*slant;
x1=.5[x2,x4]+noise;
x2=good.x(1.75u+s+noise);
x3=.5[x2,x4]+noise;
w-x4=good.x(2u+s+noise);  
x5=.618[x4,x2]+noise;
x6=leftstemloc+noise;
top y1=h+o+noise; 
y2=.5[y1,y3]+noise;
y4=.5[y1,y3]+noise;
bot y5=noise;
y3=barheight+noise;
z6=z5+whatever*randrt;
pair randir;
randir:=randup;
draw full(z1,-randrt,z2,-randup,z3,randrt,z4,randir);
draw arc(z4,-randir,z5,z6-z5)
 & z5--z6;
labels(1,2,3,4,5,6); 
endchar;

% punctation

ffmchar("visiblespace",6,ht#,comma_depth#);
lft x1=good.x(.5u+noise);
lft x2=good.x(.5u+noise);
rt x3=good.x(w-.5u+noise);
rt x4=good.x(w-.5u+noise);
top y1=o+noise;
top y4=o+noise;
bot y2=noise-d;
bot y3=noise-d;
draw z1--z2--z3--z4;
labels(1,2,3,4); 
endchar;

ffmchar("period",5,x_ht#,0);
x1=x2=.5w+noise; 
bot y1=noise-o;
y2=y1+dotincr*py;
draw dotcircle(z1,z2);
labels(1,2); 
endchar;

ffmchar("colon",5,x_ht#,0);
italcorr .8barheight#*slant;
x1=x2=.5w+noise;
x3=x4=.5w+noise;
bot y1=noise-o;
y2=y1+dotincr*py;
y3=y4-dotincr*py=barheight+noise;
draw dotcircle(z1,z2);
draw dotcircle(z3,z4);
labels(1,2,3,4); 
endchar;

ffmchar("comma",6,x_ht#,comma_depth#);
x1=leftstemloc; 
x2=w-x1;
top y2=.382barheight;
bot y1=-d;
draw z1--z2;
labels(1,2); 
endchar;

ffmchar("semicolon",6,x_ht#,comma_depth#);
italcorr barheight#*slant;
x1=leftstemloc+noise; 
w-x2=leftstemloc+noise; 
w-x3=w-x4=leftstemloc+noise; 
y3=y4-dotincr*py=barheight;
top y2=.382y3;
bot y1=-d;
draw z1--z2;
draw dotcircle(z3,z4);
labels(1,2,3,4); 
endchar;

ffmchar("exclam",5,ht#,0);
italcorr .8ht#*slant;
x1=x2=.5w+noise; 
x3=.5w+noise; 
x4=.5w+noise; 
bot y1=noise-o; 
y2=y1+dotincr*py;
top y4=h+o+noise;
bot y3=max(.618barheight,top y2+eps)+noise; 
draw dotcircle(z1,z2);
draw z3--z4; 
labels(1,2,3,4); 
endchar;

ffmchar("question",12,ht#,0);
italcorr .8ht#*slant;
x1=good.x(1.5u+s+noise);
w-x3=good.x(2u+s+noise);
x5=.618[x3,x1]+noise; 
x2=.618[x1,x3]+noise; 
x4=.618[x1,x3]+noise; 
x6=x7=.618[x3,x1]+noise; 
top y1=h+noise; 
top y2=h+noise; 
y4=barheight+noise; 
y3=.5[y2,y4]+noise;
bot y6=noise-o;
y7=y6+dotincr*py;
bot y5=max(.618y4,top y7+eps)+noise; 
pair randir;
randir:=-randrt;
draw z1--z2
 & half(z2,z2-z1,z3,-randup,z4,randir)
 & arc(z4,randir,z5,-randup);
draw dotcircle(z6,z7);
labels(1,2,3,4,5,6,7); 
endchar;

ffmchar("hyphen",6,x_ht#,0);
italcorr .618x_ht#*slant;
lft x1=noise; 
rt x2=w+noise;
y1=.618h+noise;
y2=.618h+noise;
draw z1--z2;
labels(1,2); 
endchar;

ffmchar("emdash",18,x_ht#,0);
italcorr .618x_ht#*slant;
lft x1=noise; 
rt x2=w+noise;
y1=.618h+noise;
y2=.618h+noise;
draw z1--z2;
labels(1,2); 
endchar;

ffmchar("endash",9,x_ht#,0);
italcorr .618x_ht#*slant;
lft x1=noise; 
rt x2=w+noise;
y1=.618h+noise;
y2=.618h+noise;
draw z1--z2;
labels(1,2); 
endchar;

ffmchar("cwm",0,x_ht#,0);
endchar;

ffmchar("ampersand",15,ht#,0);
italcorr ht#*slant;
x3=good.x(1.5u+s+noise);
x6=good.x(1.5u+s+noise);
rt x12=w-eps;
x1=.618w+noise;
x11=.618w+noise;
x10=.5[x11,x12]+noise;
x9=.5[x11,x12]+noise;
x2=.618[x9,x6]+noise;
x4=.618[x9,x6]+noise;
x7=.618[x9,x6]+noise;
x8=.618[x7,x9]+noise;
x5=x4+.1u;
top y1=h+.5noise;
top y2=h+.5noise;
bot y7=.5noise;
bot y8=.5noise;
y4=barheight+noise;
y11=barheight+noise;
y12=barheight+noise;
y3=.5[y2,y4]+noise;
y6=.5[y4,y7]+noise;
y9=.5[y4,y7]+noise;
z5=z4+whatever*randrt;
z10=whatever[z11,z12];
draw z1--z2
 & half(z2,z2-z1,z3,-randup,z4,z5-z4)
 & z4--z5;
draw half(z4,z4-z5,z6,-randup,z7,z8-z7)
 & z7--z8
 & arc(z8,z8-z7,z9,z10-z9)
 & z9--z10;
draw z11--z12;
labels(1,2,3,4,5,6,7,8,9,10,11,12); 
endchar;
